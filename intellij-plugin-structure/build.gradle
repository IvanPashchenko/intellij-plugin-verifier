plugins {
  id "maven-publish"
  id "org.jetbrains.kotlin.jvm" version "1.3.70"
}

ext.kotlin_version = '1.3.70'

def intellijPluginStructureVersion = 'dev'
if (project.hasProperty("structureVersion")) {
  intellijPluginStructureVersion = project.structureVersion
}

configure(allprojects) {
  group = 'org.jetbrains.intellij.plugins'
  version = intellijPluginStructureVersion

  apply plugin: 'java'
  apply plugin: 'kotlin'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
    maven { url "https://repo.maven.apache.org/maven2" }
    maven { url "https://www.jetbrains.com/intellij-repository/releases" }
    maven { url "https://packages.jetbrains.team/maven/p/ij/intellij-dependencies" }
  }

  dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.11.0"
  }

  //build .jar with sources
  task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  artifacts {
    archives sourcesJar
  }

  compileKotlin {
    kotlinOptions {
      apiVersion = '1.3'
    }
  }
}

publishing {
  publications {

    def configurePublication = { MavenPublication pub, String projectName ->

      def proj = project(projectName)
      pub.groupId proj.group
      pub.artifactId proj.name
      pub.version proj.version

      pub.from proj.components.java
      proj.artifacts
      pub.artifact proj.sourcesJar
    }

    BasePublication(MavenPublication) { configurePublication(it, ':structure-base') }
    ClassesPublication(MavenPublication) { configurePublication(it, ':structure-classes') }
    IntellijPublication(MavenPublication) { configurePublication(it, ':structure-intellij') }
    IntellijClassesPublication(MavenPublication) { configurePublication(it, ':structure-intellij-classes') }
    IdePublication(MavenPublication) { configurePublication(it, ':structure-ide') }
    IdeClassesPublication(MavenPublication) { configurePublication(it, ':structure-ide-classes') }
    TeamCityPublication(MavenPublication) { configurePublication(it, ':structure-teamcity') }
    DotNetPublication(MavenPublication) { configurePublication(it, ':structure-dotnet') }
    HubPublication(MavenPublication) { configurePublication(it, ':structure-hub') }
    EduPublication(MavenPublication) { configurePublication(it, ':structure-edu') }
    FleetPublication(MavenPublication) { configurePublication(it, ':structure-fleet') }
    KtorPublication(MavenPublication) { configurePublication(it, ':structure-ktor') }
  }

  repositories {
    maven {
      url "https://packages.jetbrains.team/maven/p/intellij-plugin-verifier/intellij-plugin-structure"
      credentials {
        username = project.hasProperty('publishUser') ? project.property('publishUser') : System.getenv('PUBLISH_USER')
        password = project.hasProperty('publishPassword') ? project.property('publishPassword') : System.getenv('PUBLISH_PASSWORD')
      }
    }
  }
}

test.dependsOn project(":tests").test
publishToMavenLocal.dependsOn test
publish.dependsOn test